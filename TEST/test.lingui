LANGUAGE

    MainMenu

    :Chests count

    :Swords count

    :NoSwords
        Swords( 0* )

    :OneSword
        Swords( 1* )

    TheItems items

    TheItemsHaveBeenFound items

    Dump this
        "\"" ~ $this ~ "\" / \"" ~ *this ~ "\" / '" ~ ^this ~ "' / "
        if :this
            GetIntegerText( #this ) ~ " / "
        if .this
            GetRealText( %this ) ~ " / "

        GetPluralityText( @this ) ~ " / " ~ GetPluralityText( °this ) ~ " / " ~ GetGenreText( &this ) ~ "\n"

    TestFunctions
        GetIntegerText( -12 ) ~ " / " ~ GetRealText( -12.0, -1 ) ~ " / " ~ GetRealText( -12.0 ) ~ " / " ~ GetRealText( -12.0, 3 ) ~ " \n"
        GetRealText( -12.3, 3, 3, '_' ) ~ " / " ~ GetRealText( -12.345 ) ~ " / " ~ GetRealText( -12.3456789, 0, 3, DotCharacter ) ~ "\n"
        GetLowerCase( "jack SPARROW" ) ~ " / " ~ GetUpperCase( "john MCLANE" ) ~ "\n"
        GetSentenceCase( "jason bourne" ) ~ " / " ~ GetTitleCase( "james kirk" ) ~ "\n"

        Dump( MakeTranslation( "cm" ) )
        Dump( MakeTranslation( "cm", "0" ) )
        Dump( MakeTranslation( "cm", "1" ) )
        Dump( MakeTranslation( "cm", "2" ) )
        Dump( MakeTranslation( "cm", "-12.345" ) )
        Dump( MakeTranslation( "cm", "-12.345", male ) )
        Dump( MakeTranslation( 12 ) )
        Dump( MakeTranslation( 12, female ) )

        Dump( 3* )
        Dump( 4*"perros" )
        Dump( 5*"fiestas":female )
        Dump( 6.5* )
        Dump( 7.5*"metros" )
        Dump( 8.5*"vueltas":female )

    Test
        var no_chests one_chest
        no_chests = Chests( 0* )
        one_chest = Chests( 1* )

        TheItemsHaveBeenFound( no_chests )
        TheItemsHaveBeenFound( one_chest )
        TheItemsHaveBeenFound( Chests( 2* ) )
        TheItemsHaveBeenFound( NoSwords() )
        TheItemsHaveBeenFound( OneSword() )
        TheItemsHaveBeenFound( Swords( 2* ) )

        TestFunctions()

ENGLISH_LANGUAGE : LANGUAGE

    MainMenu
        "Main menu"

    :Chests count
        if #count = 1
            "chest"
        else
            "chests"
        *result = *count

    :Swords count
        if @count = one
            "sword"
        else
            "swords"
        *result = *count

    TheItems items
        if #items = 0
            "No "
        elseif #items = 1
            "The "
        else
            "The "
            *items
            " "
        items

    TheItemsHaveBeenFound items
        TheItems( items )
        if #items = 1
            " has"
        else
            " have"
        " been found.\n"

FRENCH_LANGUAGE : LANGUAGE

    MainMenu
        "Menu principal"

    :Chests count
        if #count <= 1
            "coffre"
        else
            "coffres"
        *result = *count
        &result = male

    :Swords count
        if @count = one
            "épée"
        else
            "épées"
        *result = *count
        &result = female

    TheItems items
        if #items = 0
            if &items = female
                "Aucune "
            else
                "Aucun "
        elseif #items = 1
            if HasFirstCharacter( GetLowerCase( $items ), "aâeéêèiîoôuûh" )
                "L'"
            elseif &items = female
                "La "
            else
                "Le "
        else
            "Les "
            *items
            " "
        items

    Have items
        if #items = 0
            " n'a"
        elseif #items <= 1
            " a"
        else
            " ont"

    BeenFound items
        " été trouvé"
        if &items = female
            "e"
        if #items > 1
            "s"

    TheItemsHaveBeenFound items
        TheItems( items )
        Have( items )
        BeenFound( items )
        ".\n"
