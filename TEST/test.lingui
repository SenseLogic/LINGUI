Game

    MainMenu

    :Chests count

    :Swords count

    :NoSwords
        Swords( 0* )

    :OneSword
        Swords( 1* )

    TheItems items

    TheItemsHaveBeenFound items

    Test
        var no_chests one_chest
        no_chests = Chests( 0* )
        one_chest = Chests( 1* )
        TheItemsHaveBeenFound( no_chests )
        TheItemsHaveBeenFound( one_chest )
        TheItemsHaveBeenFound( Chests( 2* ) )
        TheItemsHaveBeenFound( NoSwords() )
        TheItemsHaveBeenFound( OneSword() )
        TheItemsHaveBeenFound( Swords( 2* ) )

English : Game

    MainMenu
        "Main menu"

    :Chests count
        if #count = 1
            "chest"
        else
            "chests"
        *result = *count

    :Swords count
        if @count = one
            "sword"
        else
            "swords"
        *result = *count

    TheItems items
        if #items = 0
            "No "
        elseif #items = 1
            "The "
        else
            "The "
            *items
            " "
        items

    TheItemsHaveBeenFound items
        TheItems( items )
        if #items = 1
            " has"
        else
            " have"
        " been found.\n"

French : Game

    MainMenu
        "Menu principal"

    :Chests count
        if #count <= 1
            "coffre"
        else
            "coffres"
        *result = *count
        &result = male

    :Swords count
        if @count = one
            "épée"
        else
            "épées"
        *result = *count
        &result = female

    TheItems items
        if #items = 0
            if &items = female
                "Aucune "
            else
                "Aucun "
        elseif #items = 1
            if HasFirstCharacter( GetLowerCase( $items ), "aâeéêèiîoôuûh" )
                "L'"
            elseif &items = female
                "La "
            else
                "Le "
        else
            "Les "
            *items
            " "
        items

    Have items
        if #items = 0
            " n'a"
        elseif #items <= 1
            " a"
        else
            " ont"

    BeenFound items
        " été trouvé"
        if &items = female
            "e"
        if #items > 1
            "s"

    TheItemsHaveBeenFound items
        TheItems( items )
        Have( items )
        BeenFound( items )
        ".\n"
